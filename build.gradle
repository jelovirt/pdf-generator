apply plugin: 'java'

version = '0.6.0'

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation group: 'org.apache.ant', name: 'ant', version:'1.10.12'
    implementation(group: 'com.google.guava', name: 'guava', version: '25.1-jre') {
        exclude group: 'org.checkerframework', module: 'checker-qual'
        exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        exclude group: 'com.google.j2objc', module: 'j2objc-annotations'
    }
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.2.2'
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.13.0'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.32'
    implementation group: "org.dita-ot", name: "dost", version: "[3.4,4.0)"
    implementation group: 'net.sf.saxon', name: 'Saxon-HE', version: '10.6'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.2'
    testImplementation 'org.skyscreamer:jsonassert:1.5.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

sourceSets {
    main {
        resources {
            srcDirs = ["src/generator"]
            include "**/*.xsl"
            include "**/*.json"
        }
    }
}

task dist(type: Zip, dependsOn: [jar]) {
    into("lib") {
        from ("build/libs") {
            include "*.jar"
        }
    }
    into("") {
        from ("src/main/resources") {
            include "build.xml"
        }
        from ("src/main/resources") {
            expand(
                    id: project.name,
                    version: project.version
            )
            include "plugin.xml"
        }
    }
    archiveFileName = "com.elovirta.pdf.zip"
}
